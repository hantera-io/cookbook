// This Rule engages when an order changes due to commands being applied
param input: OnOrderCommands

let deliveriesBefore = input.before.deliveries
let deliveries = input.order.deliveries

// Return the required message actor effect to complete a reservation request
let completeReservationRequest (reservationRequestId: Uuid) |>  {
  effect = 'messageActor'
  actorType = 'reservationRequest'
  actorId = reservationRequestId
  messageType = 'complete'
  body = {}
}

// Get all deliveries that were not completed before but are now completed
let completedDeliveries =
  deliveriesBefore as before
  join deliveries as delivery on deliveryId
  where m => m is (:{deliveryState: not 'completed'}, :{deliveryState: 'completed'})
  select m => m.delivery

// For each completed delivery, complete the reservation requests for each order line
from
  completedDeliveries
  select delivery =>
    delivery.orderLines
    select orderline => orderline.reservationRequestId match
      uuid |> orderline.reservationRequestId completeReservationRequest
  flatten